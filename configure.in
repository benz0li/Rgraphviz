AC_INIT(src/Rgraphviz.c)
AC_PREREQ(2.13)


AC_ARG_WITH(graphviz,
            [ --with-graphviz=DIR      root directory of graphviz installation 
                          (defaults to /usr/local/)],
GRAPHVIZ_DIR="${with_graphviz}",
GRAPHVIZ_DIR="/usr/local/")

GRAPHVIZ_VERSTR=""

dnl If user doesn't specify --with-graphviz arg, try pkg-config first
dnl and then try dotneato-config.
if test -z "${GRAPHVIZ_DIR}" ; then
   if test -z "${PKG_CONFIG}" ; then
      AC_PATH_PROG(PKG_CONFIG, pkg-config)
   fi
   if ! test -z "${PKG_CONFIG}" ; then
      GRAPHVIZ_CONFIG="$PKG_CONFIG libgvc"
      GRAPHVIZ_VERSTR="`${GRAPHVIZ_CONFIG} --modversion || echo ''`"
      if test -z "${GRAPHVIZ_VERSTR}" ; then
         AC_MSG_NOTICE(pkg-config was not able to find graphviz.  Verify graphviz is installed and that PKG_CONFIG_PATH is correct.)
      else
         PKG_CPPFLAGS="`${GRAPHVIZ_CONFIG} --cflags`"
         PKG_LIBS="`${GRAPHVIZ_CONFIG} --libs`"
      fi
   fi

   if test -z "${GRAPHVIZ_VERSTR}" ; then
      if test -z "${DOTNEATO_CONFIG}" ; then
         AC_PATH_PROG(DOTNEATO_CONFIG, dotneato-config)
      fi
      if test -z "${DOTNEATO_CONFIG}" ; then
         AC_MSG_NOTICE(dotneato-config not found in PATH.  Try specifying location of graphviz using --with-graphviz)
      else
         GRAPHVIZ_VERSTR=`${DOTNEATO_CONFIG} --version`
         PKG_CPPFLAGS="`${DOTNEATO_CONFIG} --cflags`"
         PKG_LIBS="`${DOTNEATO_CONFIG} --ldflags` `${DOTNEATO_CONFIG} --libs` -lm"
      fi
   fi
else 
    dnl User specified --with-graphviz DIR
    DOT="${GRAPHVIZ_DIR}/bin/dot"
    if ! test -x "${DOT}" ; then
       AC_MSG_ERROR($DOT not found.  Check graphviz installation.)
    else
       GRAPHVIZ_VERSTR=`${DOT} -V 2>&1 | cut -f3 -d" "`
       # Newer versions have different output from dot -V
       # this less than ideal, seems to work for graphviz 2.12
       if test "${GRAPHVIZ_VERSTR}" = "Graphviz"; then
                  GRAPHVIZ_VERSTR=`${DOT} -V 2>&1 | cut -f5 -d" "`
       fi
       PKG_CPPFLAGS="-I${GRAPHVIZ_DIR}/include/graphviz"
       # Add both dirs since actual location depends on graphviz
       # version.  Could do version computation here, but having
       # the extra path doesn't hurt.
       PKG_LIBS="-L${GRAPHVIZ_DIR}/lib/graphviz -L${GRAPHVIZ_DIR}/lib -lgvc"
    fi
fi

if test -z "${GRAPHVIZ_VERSTR}" ; then
   AC_MSG_ERROR(graphviz was not found.)
   exit 1
fi

dnl Verify we have a usable graphviz version.
dnl Currently, this means graphviz >= 2.2.x
AC_MSG_NOTICE(Found graphviz $GRAPHVIZ_VERSTR)
MAJOR=`echo ${GRAPHVIZ_VERSTR} | cut -f1 -d"."`
MINOR=`echo ${GRAPHVIZ_VERSTR} | cut -f2 -d"."`

if test ${MAJOR} -lt "2" || (test ${MAJOR} -eq "2" && test ${MINOR} -lt "2") ; then
   AC_MSG_ERROR("Need graphviz >= 2.2")
   exit 1
fi

GVIZ_DEFS="${GVIZ_DEFS} -DGRAPHVIZ_MAJOR=${MAJOR} -DGRAPHVIZ_MINOR=${MINOR}"

if test ${MAJOR} -le "2" && test ${MINOR} -le "12"; then
   AC_MSG_CHECKING([for whether compiler has bool])
   AC_HEADER_STDBOOL
   AC_TRY_COMPILE([
   #ifdef HAVE_STDBOOL_H>
   #include <stdbool.h>
   #endif
   ],[
   bool foo = true, bar = false;
   ],[
   AC_MSG_RESULT(yes)
   GVIZ_DEFS="${GVIZ_DEFS} -DHAVE_STDBOOL_H=1 -DHAVE_BOOL=1"
   ],[
   AC_MSG_RESULT(no)])
fi

AC_SUBST(GVIZ_DEFS)
AC_SUBST(PKG_CPPFLAGS)
AC_SUBST(PKG_LIBS)
AC_OUTPUT(src/Makevars)
